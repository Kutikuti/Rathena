//===== FoY Script =======================================
//= Event chaise musicale
//===== By: ==================================================
//= Kutikuti
//===== Current Version: =====================================
//= 0.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Dans une petite salle, des safety de différents lvl vont être posés par le npc, en fonction du nombre de joueur. 
//Chaque joueur a quelques secondes pour se diriger vers un safety. 
//Si deux joueurs sont sur un même safety, c’est perdu. 
//Quand le npc dit “Stop”, les joueurs sont stone curse. 
//Si les joueurs ne sont pas sur des safety a temps, c’est perdu. 
//Les Safety disparaissent chacun à leur tour, en fonction de leur lvl. 
//Le dernier joueur sur map est le gagnant. Jeu a faire sur plusieures manches.
//= 
//===== Additional Comments: =================================
//= 0.1 Création
//============================================================

prontera,161,155,4	script	Musical Chair Event	647,{

	function makeSW;function isPlayerInSW;function checkPlayersInSw;function getWinner;function freezeAll;

	set .@GMLevel,60;	// GM level required to access NPC.
	set .@n$,"[^0000FFMusical Chair NPC^000000]";

	if (getgmlevel()>=.@GMLevel) {
		mes .@n$;
		mes "Select an option.";
		next;
		switch(select("Turn ON/OFF Event")) {
		case 1:
			mes .@n$;
			if ($EventMCON) {
				mes "The Event is currently: [^0000FFON^000000]";
				mes "Would you like to turn it OFF?";
			} else {
				mes "The Event is currently: [^FF0000OFF^000000]";
				mes "Would you like to turn it ON?";
			}
			//TODO : better manage turn off
			if(select("Yes:No")==2) close;
			if ($EventMCON) {
				set $EventMCON,0; 
				set .Timer,0;
				setnpctimer 0; 
				stopnpctimer;
				mapannounce $mapMC$,"Un GM a décidé d'arrêter l'event Chaise Musicale. Aucune récompense ne sera donnée.",bc_fr;
				mapannounce $mapMC$,"A GM has decided to turn the Musical Chair Event off. As a result no further prizes will be given.",bc_en;
				close;
			}
			next;
			mes .@n$;
			mes "Input the number of rounds you want the event to last.";
			mes "Default number: [^0000FF"+.Rounds+"^000000]";
			next;
			input .@Rounds;
			set .Rounds,.@Rounds;
			mes .@n$;
			mes "The number of rounds has been changed to "+.Rounds+".";
			set $EventMCON,1; 
			set .Timer,1; 
			setnpctimer 0; 
			initnpctimer;
			announce $mapMC$,"L’Event des chaises musicales va débuter dans 3 minutes. Plus d’explications sur le discord ainsi qu’en salle de jeu.",bc_fr;
			announce $mapMC$,"The Musical Chair event will take place in 3 minutes. More information on Discord and in the event room.",bc_en;
			announce $mapMC$,"Pour rejoindre celui-ci, merci de vous diriger dans la salle Event de Dreamea et de parler au Panneau de l’événement.",bc_fr;
			announce $mapMC$,"To join, go to the event room in Dreamea and talk to the event panel.",bc_en;
			.gmrid = getattachedrid;
			close2;
		}
	}
	if ($EventMCON) end;

OnInit:
	set $EventMCON,0;
	set $roundMCON,0;
	set .Winner,0;
	set .Rounds,5;
	set .Prize,512;
	set .PrizeAmt,1;	
	set .PrizeLooser,512;
	set .PrizeAmtLooser,1;
	set $mapMC$,"prontera";
	set $spawnMCx,155;
	set $spawnMCy,155;
	set .x1,150;
	set .y1,150;
	set .x2,160;
	set .y2,160;
	set .gmrid,0;
	end;
OnTimer10000:
	if (.Timer) end;
	goto iMusicalChair;
	end;
OnTimer60000:
	if (.Timer!=1) end;
	announce $mapMC$,"L’Event des chaises musicales va débuter dans 2 minutes. Si vous souhaitez participer,",bc_fr;
	announce $mapMC$,"The Musical Chair event will take place in 2 minutes. ",bc_en;
	announce $mapMC$,"Merci de vous diriger vers la Salle Event, à Dreamea, et de cliquer sur le panneau de l’événement.",bc_fr;
	announce $mapMC$,"To join, go to the event room in Dreamea and talk to the event panel.",bc_en;
	end;
OnTimer120000:
	if (.Timer!=1) end;
	announce $mapMC$,"L’Event des chaises musicales va débuter dans 1 minutes. Si vous souhaitez participer,",bc_fr;
	announce $mapMC$,"he Musical Chair event will take place in 1 minutes. ",bc_en;
	announce $mapMC$,"Merci de vous diriger vers la Salle Event, à Dreamea, et de cliquer sur le panneau de l’événement.",bc_fr;
	announce $mapMC$,"To join, go to the event room in Dreamea and talk to the event panel.",bc_en;
	end;
OnTimer180000:
	if (.Timer!=1) end;
	announce $mapMC$,"L’Event des chaises musicales débute ! Bonne partie à tous !",bc_fr;
	announce $mapMC$,"The Musical Chair event has begun! Have fun!",bc_en;
	set .Timer,0; 
	stopnpctimer;
	setnpctimer 0; 
	initnpctimer;
	end;
iMusicalChair:
	attachrid(.gmrid);
	set .Winner,0;

	mapannounce $mapMC$,"Manche " + (.RoundCount+1) + "/" + .Rounds,bc_fr;
	mapannounce $mapMC$,"Round " + (.RoundCount+1) + "/" + .Rounds,bc_en;
	sleep2 5000;

	mapannounce $mapMC$,"L’event démarre !",bc_fr;
	mapannounce $mapMC$,"The event is starting!",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"Je vous rappelle brièvement les règles : ",bc_fr;
	mapannounce $mapMC$,"I'll explain the rules again quickly:",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"Dans cette salle va être invoqué des Safety Wall de différents niveaux.",bc_fr;
	mapannounce $mapMC$,"Safety Walls of different levels will be cast in this room.",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"A vous de choisir judicieusement le vôtre.",bc_fr;
	mapannounce $mapMC$,"Choose yours wisely.",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"Après 30 secondes, vous ne pourrez plus bouger, faites donc attention à où vous mettez les pieds !",bc_fr;
	mapannounce $mapMC$,"30 seconds later, you won't be able to move, be careful where you are staying!",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"Si vous n’avez pas de Safety Wall, c’est perdu.",bc_fr;
	mapannounce $mapMC$,"If you don't have a Safety Wall, you lost.",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"Le gagnant est celui qui restera le plus longtemps dans un safety !",bc_fr;
	mapannounce $mapMC$,"The winner is the last one standing in a Safety Wall",bc_en;
	sleep2 2000;
	mapannounce $mapMC$,"A vos marques...",bc_fr;
	mapannounce $mapMC$,"Ready...",bc_en;
	sleep2 1000;
	mapannounce $mapMC$,"Prêt…",bc_fr;
	mapannounce $mapMC$,"Set...",bc_en;
	sleep2 1000;
	mapannounce $mapMC$,"Choisissez votre Safety !",bc_fr;
	mapannounce $mapMC$,"Choose your Safety!",bc_en;
	sleep2 1000;

	$roundMCON = 1;

	makeSW();

	sleep2 30000;

	mapannounce $mapMC$,"Les 30 secondes sont écoulées !",bc_fr;
	mapannounce $mapMC$,"30 seconds are over!",bc_en;
	freezeAll();

	if(checkPlayersInSw() == 0){
		goto iCancel;
	} 

	while(.Winner == 0){
		if(checkPlayersInSw() == 1){
			.Winner = 1;
		};	
		sleep2 1000;
	}
	goto iWin;
	end;
iWin:
	if ($EventMCON!=1) {end;}
	set .RoundCount,.RoundCount+1;
	.WinnerChar$ = getWinner();
	.WinnerGID = getcharid(3,.WinnerChar$);
	attachrid(.WinnerGID);
	getitem .Prize,.PrizeAmt;
	detachrid;
	attachrid(.gmrid);
	$roundMCON = 0;
	mapannounce $mapMC$,.WinnerChar$ + " est le gagnant de cette manche !",bc_fr;
	mapannounce $mapMC$,.WinnerChar$ + " is the winner of this round !",bc_en;
	if (.RoundCount>=.Rounds) {
		set .RoundCount,0; 
		set $EventMCON,0;
		setnpctimer 0; 
		stopnpctimer;
		mapannounce $mapMC$,"Cette manche était la dernière, merci d'avoir participé !",bc_fr;
		mapannounce $mapMC$,"This was the last round, thank you for playing!",bc_en;
		end;
	}
	setnpctimer 0;
	end;
iCancel:
	if ($EventMCON!=1) {end;}
	set .RoundCount,.RoundCount+1;
	$roundMCON = 0;
	mapannounce $mapMC$,"Personne n'était positionné sur une Safety Wall, vous avez tous perdu...",bc_fr;
	mapannounce $mapMC$,"Nobody was in a Safety Wall, you all lost...",bc_en;
	if (.RoundCount>=.Rounds) {
		set .RoundCount,0; 
		set $EventMCON,0;
		setnpctimer 0; 
		stopnpctimer;
		mapannounce $mapMC$,"Cette manche était la dernière, merci d'avoir participé !",bc_fr;
		mapannounce $mapMC$,"This was the last round, thank you for playing!",bc_en;
		end;
	}
	setnpctimer 0;
	end;

//arg0 : charID
function isPlayerInSW{
	if(getarg(0,0) == 0) {return 0;}
	.charID = getarg(0,0);
	return getstatus(SC_SAFETYWALL,0,.charID);
}

function makeSW{
	.players = getareaunits(BL_PC,$mapMC$,.x1,.y1,.x2,.y2,.@array$[0]);

	for ( .i=1 ; .i <= .players ; .i++ ) {
		//todo : empecher de spawn safety au même endroit
		.x = rand(.x1,.x2);
		.y = rand(.y1,.y2);
		unitskillusepos getnpcid(0),12,(6+.i),.x,.y,-100;
		sleep2 50;
	}
}

function freezeAll{
	.@num = getareaunits(BL_PC,$mapMC$,.x1,.y1,.x2,.y2,.@array$[0]);
	for(.@i=0;.@i<getarraysize(.@array$);.@i++){
		.charname$ = .@array$[.@i];
		.gid = getcharid(3,.charname$);
		pcblockmove .gid , 1;
		pcblockskill .gid , 1;
	}
}

function checkPlayersInSw{
	.@num = getareaunits(BL_PC,$mapMC$,.x1,.y1,.x2,.y2,.@array$[0]);

	set .playersinSW, 0;

	for(.@i=0;.@i<getarraysize(.@array$);.@i++){
		.charname$ = .@array$[.@i];
		.charid = getcharid(0,.charname$);
		.gid = getcharid(3,.charname$);
		.inSW = isPlayerInSW(.charid);
		if (.inSW == 1) { 
			.playersinSW = .playersinSW + 1; 
		} else{
			mapannounce $mapMC$,.charname$ + " a choisi un mauvais safety, il est éliminé !",bc_fr;
			mapannounce $mapMC$,.charname$ + " chose poorly, he is eliminated!",bc_en;
			attachrid(.gid);
			getitem .PrizeLooser,.PrizeAmtLooser;
			detachrid;
			attachrid(.gmrid);
			warp "prontera",150,142,.charid;
			pcblockmove .gid , 0;
			pcblockskill .gid , 0;
		}
	}

	return .playersinSW;
}

function getWinner{
	.@num = getareaunits(BL_PC,$mapMC$,.x1,.y1,.x2,.y2,.@array$[0]);
	.charname$ = .@array$[0];
	.charid = getcharid(0,.charname$);
	.gid = getcharid(3,.charname$);

	pcblockmove .gid , 0;
	pcblockskill .gid , 0;

	return .@array$[0];
}

}

prontera,150,140,4	script	Musical Chair Helper	837,{
	set .@n$,"[^0000FFMusical Chair Helper^000000]";
	if 	($EventMCON == 1 && $roundMCON == 0){
		mes .@n$;
		mes @langUser?"Règles du jeu :":"Game rules:";
		mes @langUser?"Dans cette salle va être invoqué autant de Safety Wall que de joueurs. A vous de choisir votre safety avec précaution. Après 30 secondes, vous ne pourrez plus bouger, faites donc attention à où vous mettez les pieds !":"Safety Walls of different levels will be cast in this room. Choose yours wisely. 30 seconds later, you won't be able to move, be careful where you are staying!";
		next;
		mes .@n$;
		mes @langUser?"Quand le top est lancé, les safety vont progressivement disparaître car ils seront de différents niveau. Si le votre disparaît, vous avez perdu. Le dernier dans la salle a gagné. Si vous n’avez choisi aucun safety, vous avez perdu.":"Safety Walls will progressively disapear as they are from different level. If yours disapear, you lost. The last one standing is the winner. If you didn't choose a Safety, you lost.";
		next;
		mes .@n$;
		mes @langUser?"Souhaitez-vous participer ?":"TODO";
		.@switch$ = @langUser?"Participer:Partir":"Participate:Leave";
		switch(select(.@switch$)){
		case 1: 
			warp $mapMC$,$spawnMCx,$spawnMCy;
		case 2:
			close;
		}
	} else if ($EventMCON == 1 && $roundMCON == 1){
		mes .@n$;
		mes @langUser?"Vous ne pouvez pas rejoindre l'event pendant qu'une manche est en cours.":"You can't join the event while a round is in progress.";
		close;
	} else{
		mes .@n$;
		mes @langUser?"L'event Chaise Musicale n'est pas en cours.":"The Musical Chair event is not in progress.";
		close;
	}
}
